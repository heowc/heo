plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.graalvm.buildtools.native' version '0.9.16'
    id 'maven-publish'
}

group = 'com.heowc'
version = '0.0.1'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

ext {
    set('springShellVersion', "3.2.3")
}

dependencies {
    implementation 'org.springframework.shell:spring-shell-starter'
    implementation 'guru.nidi:graphviz-java:0.18.1'
    implementation 'guru.nidi:graphviz-java-min-deps:0.18.1'

    implementation 'org.jgrapht:jgrapht-core:1.5.2'
    implementation 'org.jgrapht:jgrapht-io:1.5.2'

    implementation 'com.github.javaparser:javaparser-core:3.25.10'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.shell:spring-shell-test'
    testImplementation 'org.springframework.shell:spring-shell-test-autoconfigure'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
    }
}

graalvmNative {
    metadataRepository {
        enabled = true
    }
    binaries {
        main {
            buildArgs.add('--verbose')
            buildArgs.add('-H:IncludeResources=.*js$')
            buildArgs.add("-H:ReflectionConfigurationFiles=${projectDir}/src/main/resources/META-INF/native-image/reflect-config.json")
            buildArgs.add("-H:ResourceConfigurationFiles=${projectDir}/src/main/resources/META-INF/native-image/resource-config.json")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/heowc/heo")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}