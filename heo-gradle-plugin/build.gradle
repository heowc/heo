plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

gradlePlugin {
    website = 'https://github.com/heowc/heo'
    vcsUrl = 'https://github.com/heowc/heo.git'
    plugins {
        heoPlugin {
            id = 'dev.heowc.heo'
            displayName = 'heo gradle plugin'
            description = 'heo-cli is provided as a gradle plugin'
            tags.set(['heo', 'static analysis'])
            implementationClass = 'dev.heowc.heo.gradle.HeoPlugin'
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

task copyJar(type: Copy) {
    from "${rootDir}/heo-cli/build/libs/heo-cli-${version}.jar"
    into "${projectDir}/src/main/resources"
    rename { String fileName ->
        fileName.replace("heo-cli-${version}.jar", 'heo-cli.jar')
    }
}

tasks.processResources.dependsOn(copyJar)
tasks.copyJar.dependsOn(":heo-cli:bootJar")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }
}
